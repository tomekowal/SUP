#!/usr/bin/env escript
%% -*- erlang -*-

-define(SCRIPT_RIGHTS, "0775").

main(Arguments) ->
    file:set_cwd(filename:dirname(escript:script_name())),
    main(Arguments, load_config()).

main([], Config) ->
    PackageName = base_package_name(Config),
    Variables = get_variables(Config),

    execute_command("rm -rf "++PackageName),
    initialize_pkg_dirs(Config),
    file:set_cwd(PackageName),

    save("DEBIAN/control", fill_template("../templates/control-base", Variables)),
    save("DEBIAN/prerm", fill_template("../templates/prerm-base", Variables), ?SCRIPT_RIGHTS),

    TargetRoot = config_get(Config, target_root),
    SourceDir = config_get(Config, source_dir),
    Result = execute_commands(
               [
                "cp -R ../"++SourceDir++"/* ."++TargetRoot,
                "rm -rf ."++TargetRoot++"/lib/*",
                "rm -rf ."++TargetRoot++"/releases/*",
                "md5sum `find . -type f | awk '/."++escape_slashes(TargetRoot)++"/ { print substr($0, 3) }'` > DEBIAN/md5sums",
                "dpkg -b . .."
               ]
              ),

    case Result of
        0 ->
            io:format("*** Package building successful ***~n"),
            ok;
        _ ->
            io:format("*** Package building failed ***~n"),
            failed
    end;
main(_, _Config) ->
    io:format("Usage: ~s~n", [filename:basename(escript:script_name())]).

load_config() ->
    {ok, Config} = file:consult("debian.config"),
    Config.

config_get(Config, Key) ->
    {Key, Value} = lists:keyfind(Key, 1, Config),
    Value.

get_variables(Config) ->
    Nodename = config_get(Config, nodename),
    [ErtsDir] = filelib:wildcard(config_get(Config, source_dir)++"/erts-*"),
    ErtsVersion = string:substr(filename:basename(ErtsDir), length("erts-")+1),
    PackageName = base_package_name(Config),
    Architecture = os:cmd("dpkg-architecture -c 'echo -n $DEB_HOST_ARCH'"),

    [
     {target_root, config_get(Config, target_root)},
     {package, PackageName},
     {vsn, ErtsVersion},
     {architecture, Architecture},
     {maintainer, config_get(Config, maintainer)},
     {description, "Base Erlang embedded node runtime for SUP-managed release "++Nodename}
    ].

initialize_pkg_dirs(Config) ->
    PackageName = base_package_name(Config),

    os:cmd("mkdir -p "++PackageName++"/DEBIAN"),
    os:cmd("mkdir -p "++PackageName++config_get(Config, target_root)).

base_package_name(Config) ->
    config_get(Config, nodename)++"-base".

escape_slashes(String) ->
    {ok, SlashRegex} = re:compile("/"),
    re:replace(String, SlashRegex, "\\\\/", [global, {return, list}]).

save(File, Contents) ->
    {ok, FileDevice} = file:open(File, [write]),
    io:fwrite(FileDevice, "~s", [Contents]),
    ok = file:close(FileDevice).
save(File, Contents, Rights) ->
    save(File, Contents),
    os:cmd("chmod "++Rights++" "++File),
    ok.

fill_template(TemplateFile, Variables) ->
    erlydtl:compile(TemplateFile, template),
    {ok, Result} = template:render(Variables),
    string:join(Result, "").

execute_command(Command) ->
    io:format("> ~s~n", [Command]),
    Result = os:cmd(Command),
    io:format("~s", [Result]),
    list_to_integer(os:cmd("echo -n $?")).

execute_commands([]) ->
    0;
execute_commands([Command|Rest]) ->
    case execute_command(Command) of
        0 ->
            execute_commands(Rest);
        NonzeroExitCode ->
            NonzeroExitCode
    end.
