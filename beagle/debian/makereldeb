#!/usr/bin/env escript
%% -*- erlang -*-

-define(REL_DIR, "../rel/beagle-full").
-define(TARGET_ROOT, "/home/ghik/Inz/SUP/beagle/rel/beagle-raw").
-define(SCRIPT_RIGHTS, "0775").

main([Release, Version]) ->
    file:set_cwd(filename:dirname(escript:script_name())),
    PackageName = release_package_name(Release),

    Variables = get_variables(Release, Version),
    execute_command("rm -rf "++PackageName),
    initialize_pkg_dirs(Release),
    file:set_cwd(PackageName),

    save("DEBIAN/control", fill_template("../templates/control-rel", Variables)),
    save("DEBIAN/prerm", fill_template("../templates/prerm-rel", Variables), ?SCRIPT_RIGHTS),
    save("DEBIAN/postrm", fill_template("../templates/postrm-rel", Variables), ?SCRIPT_RIGHTS),
    save("DEBIAN/preinst", fill_template("../templates/preinst-rel", Variables), ?SCRIPT_RIGHTS),
    save("DEBIAN/postinst", fill_template("../templates/postinst-rel", Variables), ?SCRIPT_RIGHTS),

    ArchiveFile = "."++filename:join([?TARGET_ROOT, "debian/releases", Release]),
    Result = execute_commands(
               [
                "tar czf "++ArchiveFile++".tar.gz -C ../"++?REL_DIR++" releases/"++Version,
                "mv "++ArchiveFile++".tar.gz "++ArchiveFile,
                "md5sum `find . -type f | awk '/."++escape_slashes(?TARGET_ROOT)++"/ { print substr($0, 3) }'` > DEBIAN/md5sums",
                "dpkg -b . .."
               ]
              ),

    case Result of
        0 ->
            io:format("*** Package building successful ***~n"),
            ok;
        _ ->
            io:format("*** Package building failed ***~n"),
            failed
    end;
main(_) ->
    io:format("Usage: ~s release version~n", [filename:basename(escript:script_name())]).

get_variables(Release, Version) ->
    RelDir = ?REL_DIR++"/releases/"++Version,
    {ok, [RelFileContents]} = file:consult(RelDir++"/"++Release++".rel"),
    {release, {Release, Version}, {erts, ErtsVersion}, Applications} = RelFileContents,

    PackageName = release_package_name(Release),
    Dependencies = [base_package_name(Release)++" (= "++ErtsVersion++")"]
        ++lists:map(
            fun({App, AppVer}) ->
                    application_package_name(Release, atom_to_list(App))++" (= "++AppVer++")"
            end,
            Applications
           ),

    [
     {target_root, ?TARGET_ROOT},
     {erts_vsn, ErtsVersion},
     {release, Release},
     {package, PackageName},
     {vsn, Version},
     {description, "SoftwareUpdatePlatform-managed release: "++Release},
     {dependencies, Dependencies}
    ].

initialize_pkg_dirs(Release) ->
    PackageName = release_package_name(Release),

    os:cmd("mkdir -p "++PackageName++"/DEBIAN"),
    os:cmd("mkdir -p "++PackageName++?TARGET_ROOT++"/debian/releases").

release_package_name(Release) ->
    Release.

application_package_name(Release, ApplicationName) ->
    {ok, UnderscoreRegex} = re:compile("_"),
    Release++"-"++re:replace(ApplicationName, UnderscoreRegex, "-", [global, {return, list}]).

base_package_name(Release) ->
    Release++"-base".

escape_slashes(String) ->
    {ok, SlashRegex} = re:compile("/"),
    re:replace(String, SlashRegex, "\\\\/", [global, {return, list}]).

save(File, Contents) ->
    {ok, FileDevice} = file:open(File, [write]),
    io:fwrite(FileDevice, "~s", [Contents]),
    ok = file:close(FileDevice).
save(File, Contents, Rights) ->
    save(File, Contents),
    os:cmd("chmod "++Rights++" "++File),
    ok.

fill_template(TemplateFile, Variables) ->
    erlydtl:compile(TemplateFile, template),
    {ok, Result} = template:render(Variables),
    string:join(Result, "").

execute_command(Command) ->
    io:format("> ~s~n", [Command]),
    Result = os:cmd(Command),
    io:format("~s", [Result]),
    list_to_integer(os:cmd("echo -n $?")).

execute_commands([]) ->
    0;
execute_commands([Command|Rest]) ->
    case execute_command(Command) of
        0 ->
            execute_commands(Rest);
        NonzeroExitCode ->
            NonzeroExitCode
    end.
